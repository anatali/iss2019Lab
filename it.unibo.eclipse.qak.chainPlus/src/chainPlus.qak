System chainPlus

Event    local_buttonCmd : local_buttonCmd(X)  
Event    ledCmd : ledCmd(X)  

Context ctxButton      ip [host="192.168.43.229" port=8010]
Context ctxLedsPlus    ip [host="192.168.43.229" port=8050]
Context ctxLedRasp     ip [host="192.168.43.18" port=8040]

QActor button context ctxButton {	//generates local_buttonCmd 
	State s0 initial { 
		run resources.buttonForChain.create()                      
	} 
}

QActor control context ctxButton {
	State s0 initial {		    
		printCurrentMessage	             
	} 
	Transition t0 whenEvent local_buttonCmd -> sOn
	State sOn{  
 		emit  ledCmd : ledCmd(on)
	}
	Transition t1 whenTime 500 -> sOff 
				  whenEvent local_buttonCmd -> s0           
 
	State sOff{   
 		emit   ledCmd : ledCmd(off)
	}
	Transition t2 	whenTime 500 -> sOn  
   		          	whenEvent local_buttonCmd -> s0
}   


QActor dynamo context ctxLedsPlus {
	State s0 initial{   
		//println("dynamo START")
	} 
	Transition t0 whenTime 3000 -> sAdd         
	   
	State sAdd{    
		run resources.ledManager.addNewLed()
	} 
	Transition t0 whenTime 4000 -> sRemove
	 
	State sRemove{
		run resources.ledManager.removeLed()
	} 
	Goto s0
	
	 
}  
  
CodedQActor led1 context ctxLedsPlus className "resources.LedActork"  
CodedQActor led2 context ctxLedsPlus className "resources.LedOnArduinoActork" 
CodedQActor led3 context ctxLedRasp  className "resources.LedOnRasp" 