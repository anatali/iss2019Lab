/*
 * ------------------------------------------------------
 * This is the first model of the system
 * written as the result of the requirement analysis
 * ------------------------------------------------------
 */  
System ddrworkermvc   

mqttBroker "127.0.0.1" : 1883    //192.168.1.18
 	  
/*
 * 1) Information exchanged 
 */
Event    envCond   : envCond( CONDTYPE )            //from the environment (simulated)
Event    sonar     : sonar(SONAR, DISTANCE)	        //from sonar in the robot environment   
Event    sonarRobot: sonar( DISTANCE )	            //from  sonar on robot 

Event  local_userCmd   : userCmd(X)		            //from GUI to console X = w | a | s | d | h 
Dispatch robotCmd  : robotCmd(X)	                //from console to robot

Event polar             : p( Distance, Angle )            
Event local_modelChange : modelChange( X )
/*
 * 2) Computational Nodes   
 */
Context ctxRobotMvc     ip [host="localhost"   port=8025]  
Context ctxSonarMvc     ip [host="localhost"   port=8025]  -mqtt 
    
/*
 * 3) System Actors
 */
 
/*
 * console: converts a userCmd into a robotCmd
 * REQ : human user interaction, sendMoveCmds
 */
QActor consolemvc context ctxRobotMvc{  
	State s0 initial {		 
		run resources.guiMvcSupport.create( myself , "msg")   //(1) REQ: human user interaction 	
		//emits  local_userCmd : userCmd( X )	X = w | a | s | d | h 
	}
}

QActor resourcemodel context ctxRobotMvc{
	State s0 initial {		
		solve( consult("sysRules.pl")	 )       
		solve( consult("ddrWorkerResourceModel.pl")	 )
		solve( showResourceModel ) 		
	}
	Goto waitModelChange
	
	State waitModelChange{}
	Transition t0 
		whenEvent local_userCmd     -> robotMove
		whenEvent local_modelChange -> changeModel
	
	State robotMove { 
		//printCurrentMessage
		onMsg( local_userCmd : userCmd( X ) ) {
			solve( action(robot, move($payloadArg(0))) )  
			forward robotmvc -m robotCmd : robotCmd( $payloadArg(0) )
		}
	}
	Goto waitModelChange
	
	State changeModel{
		printCurrentMessage
		onMsg( local_modelChange : modelChange( V ) ) {
			solve(  $payloadArg(0) ) 
		} 
	}
	Goto waitModelChange
}
/*
 * robot: executes robotCmd and handles envCond events
 * REQ : sendMoveCmds.move, handleConds
 */
QActor robotmvc context ctxRobotMvc{   
	State s0 initial {		
		println("robotplayer STARTS")	
		run resources.robotSupport.create( myself, "virtual" ) //(2) REQ : virtual or realmbot	
	}
	Goto waitCmd
	
	State waitCmd{  } 
	Transition t0 whenMsg  robotCmd    -> handleCmd
	              whenEvent envCond    -> handleCond
	              whenEvent sonarRobot ->  handleCond  //the robot hits
	
	State handleCmd{
		onMsg ( robotCmd : robotCmd(CMD) ){			
			run resources.robotSupport.move( "msg(${payloadArg(0)})"  )
			//println("msg(${payloadArg(0)})")
		}	
	}
	Goto waitCmd
	
	State handleCond{
		printCurrentMessage
		run resources.robotSupport.move( "msg(stop)"  ) //do the action
		emit local_modelChange : modelChange( changeModel( actuator, robot, state(stop) ) )	        //updates the model
	}
	Goto waitCmd
}

/*
 * sonarhandler: looks at sonar events and sends polar to radar
 * REQ : receiveSensorInfo, showSonarData
 */
QActor sonarhandlermvc context ctxSonarMvc{
 	State init initial{
		println("sonardatahandler STARTS ")
	}         
	Goto waitForEvents
	   
	State waitForEvents{  }      
   	Transition t0 whenEvent sonar      ->  sendToRadar
   	              whenEvent sonarRobot ->  sendToRadar
   	               
 	State sendToRadar{                      
 		printCurrentMessage                                        
 		onMsg ( sonar  : sonar( SONAR, DISTANCE ) ){    			
[" val D = Integer.parseInt( payloadArg(1) ) * 5"] 
			emit polar : p( $D, 90  )  
 		}
 		onMsg ( sonarRobot : sonar( DISTANCE ) ){     
[" val D = Integer.parseInt( payloadArg(0) ) * 5"] 
			emit polar : p( $D, 180  ) 
 		}
 	} 
 	Goto waitForEvents
}

/*
 * Introspector
 */
QActor introspectormvc context ctxRobotMvc{
	State init initial{
		solve( consult("sysRules.pl")	 )       
		solve( consult("ddrworkermvc.pl")	 )  
		solve( showSystemConfiguration )
	}
}
/*
 * radar: show polar data on a radar GUI (radarPojo)
 * REQ : showSonarData

QActor radarreq context ctxRobotReq{  //should work in ctxRadar
	State s0 initial {		
		//run resources.radarSupport.activate()	
		run radarPojo.radarSupport.setUpRadarGui()  //(3)	REUSE radarPojo
	}
	Goto waitMsg
	
	State waitMsg{}
	Transition t0 whenMsg   polar -> showPoint
	
	State showPoint{
		printCurrentMessage
		onMsg ( polar : p( D,A )){
			 run radarPojo.radarSupport.update( payloadArg(0), payloadArg(1)  )			 
		}
	}
	Goto waitMsg
}
 */
