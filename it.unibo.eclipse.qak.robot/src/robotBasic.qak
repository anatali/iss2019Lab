System robotBasic   

mqttBroker "127.0.0.1" : 1883    //192.168.1.18

Event    local_buttonCmd : local_buttonCmd(X)  
Event    collision : collision(X)
Event    sonar  	 : sonar(SONAR, TARGET, DISTANCE)	//From (virtual) sonar
Event    sonarDetect : sonarDetect(X)					//From (virtual robot) sonar
Event    realSonar   : sonar( DISTANCE )				//From real sonar on real robot
Event    polar       : p( Distance, Angle )  
Dispatch polar       : p( Distance, Angle )

Dispatch sendToRobot : msg(X)
Dispatch robotCmd    : robotCmd(X)

Context ctxRobot ip [host="localhost" port=8010]
Context ctxRadar ip [host="localhost" port=8015] //192.168.1.18 -mqtt

CodedQActor clienttcp context ctxRobot className "resources.clientWenvTcp"

QActor button context ctxRobot {	
	State s0 initial { 
		run resources.buttonEventEmitter.create()  //generates local_buttonCmd                     
	}  
	Goto waitForUserCommand
	
	State waitForUserCommand{} 
	Transition t0 whenEvent local_buttonCmd -> handleCmd  
	
	State handleCmd{
		printCurrentMessage
		onMsg ( local_buttonCmd : local_buttonCmd(CMD) ){
			forward robotcontrol -m robotCmd : robotCmd(  $payloadArg(0)   )
		}		
	}
	Goto waitForUserCommand
} 

QActor robotcontrol context ctxRobot{ 
	State s0 initial {		 
 	}   
	Transition t0 whenMsg robotCmd -> handleCmd  
	
 	State handleCmd{
 		printCurrentMessage
		onMsg ( robotCmd : robotCmd(CMD) ){			
			forward clienttcp -m sendToRobot : msg(   $payloadArg(0)    ) 
		}
	}
 	Transition t1  whenEvent robotCmd  -> handleCmd 
		           whenEvent collision -> handleCollision
		          
	State handleCollision{ 
		printCurrentMessage
		forward clienttcp -m sendToRobot : msg( stop  )
		forward clienttcp -m sendToRobot : msg( moveleft  )
		//forward clienttcp -m send : msg( moveleft  )
 	}
	Goto s0 
	          
 }
 
 /*
  * 
  */
 QActor sonardetector context ctxRobot{
 	State init initial{
		println("sonardetector STARTS ")
	}         
	Goto waitForEvents
	   
	State waitForEvents{  }      
   	Transition t1 whenTime 60000 -> endOfJob   
 		   whenEvent sonar       ->  sendToRadar
  		   whenEvent sonarDetect ->  showObstacle
 		  //whenEvent realSonar   ->  handleRealSonar
 	  
 	State sendToRadar{                      
 		printCurrentMessage                                        
 		onMsg ( sonar : sonar(SONAR,TARGET,DISTANCE ) ){     
			//emit polar : p( $payloadArg(2), 90  )  //90 is the angle
			[" val D = Integer.parseInt( payloadArg(2) ) * 5"] 
			forward radar -m  polar : p( $D, 90  ) 
 		}
 	} 
 	Goto waitForEvents
 	
 	State showObstacle{
 		println("sonardetector showObstacle")
 		//forward radar -m  polar : p( 70, 0  ) 
 	}
 	Goto waitForEvents
 	
 	State endOfJob{}
 }
 
 //Runs on another MQTT-based node
 //ExternalQActor radar context ctxRadar 
 
QActor radar context ctxRadar{
	
	State radarUsageInit initial{
		 run resources.radarSupport.activate()
		 solve( consult("radarData.pl")	 )
  	}
	Goto radarTest  
	 
	State radarTest{
		solve( getData(D,A)	 ) 
 		ifSolved run resources.radarSupport.spot( @D,@A  ) 
 		delay 500
	}
	Goto radarTest if "currentSolution.isSuccess()" else waitMsg
	 
	State waitMsg{}
	Transition t0 whenMsg polar -> showPoint
	
	State showPoint{
		printCurrentMessage
		onMsg ( polar : p( D,A )){
			 run resources.radarSupport.spot( payloadArg(0), payloadArg(1)  )			 
		}
	}
	Goto waitMsg
	
}
 
