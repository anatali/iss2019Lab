System robotmind

mqttBroker "localhost" : 1883    //192.168.1.18  192.168.43.229

Event  userCmd        : userCmd(X)	//from GUI X = w | a | s | d | h 
Event  envCond        : envCond( CONDTYPE )        //from the environment 
Event  modelChanged   : modelChanged( TARGET, VALUE )
Event  modelContent   : content( VALUE )


Event  sonarRobot     : sonar( DISTANCE )	     //from  sonar on robot 
Event  sonar          : sonar(SONAR, DISTANCE)	 //from sonar in the robot environment          

Dispatch modelChange  : modelChange( TARGET, VALUE )
Dispatch robotCmd     : robotCmd( CMD )

Context ctxRobotMind  ip [host="localhost" port=8035]   -mqtt
Context ctxBasicRobot ip [host="otherhost" port=8005]   -mqtt

ExternalQActor basicrobot context ctxRobotMind 

QActor resourcemodel context ctxRobotMind{

	State s0 initial {		
		solve( consult("sysRules.pl")	 )       
		solve( consult("resourceModel.pl")	 )
		solve( showResourceModel ) 		
	} 
	Goto waitModelChange
	
	State waitModelChange{ }
	Transition t0 whenMsg modelChange -> changeModel

	State changeModel{  
		printCurrentMessage 
		onMsg( modelChange : modelChange( robot,back) ) {
			solve(  action( robot, move(s) ) )  
			ifSolved  emit modelChanged : modelChanged(  robot,  s  ) 
			solve( model( A, R, STATE ) )
			["var RobotState = getCurSol(\"STATE\") "]
			emit modelContent : content( robot( $RobotState ) )
			delay 200
			solve(  action( robot, move(h) ) )  
			emit modelChanged : modelChanged(  robot,  h  )
			solve( model( A, R, STATE ) )
			["RobotState = getCurSol(\"STATE\") "]
			emit modelContent : content( robot( $RobotState ) )
		}
		onMsg( modelChange : modelChange( robot,V ) ) { // V= w | ...
			solve(  action( robot, move($payloadArg(1)) ) )  
			ifSolved emit modelChanged : modelChanged(  robot,  $payloadArg(1)  )  
			solve( model( A, R, STATE ) )
			["val RobotState = getCurSol(\"STATE\") "]
			emit modelContent : content( robot( $RobotState ) )
		} 
	}
	Goto waitModelChange
}

QActor robotmind context ctxRobotMind{   
["var obstacle = false"]
	State s0 initial {	  
		println("ROBOT MIND STARTED")	
		forward basicrobot -m robotCmd : robotCmd( a )
		delay 700
		forward basicrobot -m robotCmd : robotCmd( d )
		delay 700
		forward basicrobot -m robotCmd : robotCmd( h )
	}
	Goto waitCmd
	 
	State waitCmd {		} 
	Transition t0 whenMsg   robotCmd     -> handleCmd
		          whenEvent envCond      -> handleCmd
		          whenEvent sonarRobot   -> handleSonarRobot
		          whenEvent modelChanged -> handleModelChanged
	
	State handleCmd{  //command from the frontend
		printCurrentMessage
 		onMsg( robotCmd : robotCmd(CMD) ) {
 			forward resourcemodel -m modelChange : modelChange(robot,$payloadArg(0)) 
		}
	} 
	Goto waitCmd
 
 	State handleModelChanged{ //action to actuate
		printCurrentMessage
		onMsg ( modelChanged : modelChanged(robot,CMD) ){
			forward basicrobot -m robotCmd : robotCmd( $payloadArg(1) )
		}
	}
	Goto waitCmd
   
 	State handleSonarRobot{
 		printCurrentMessage
 		onMsg ( sonarRobot : sonar( DISTANCE ) ){
			["obstacle = Integer.parseInt( payloadArg(0) ) < 10 "]
 		}//onMsg 	 
 	}
	Goto handeObstacle if "obstacle" else waitCmd 
	
	State handeObstacle{
		println("handeObstacle: going back START")  
		forward resourcemodel -m modelChange : modelChange(robot,back) 
		/* 
		//forward basicrobot -m robotCmd : robotCmd( s )
		forward resourcemodel -m modelChange : modelChange(robot,s) //must wait the completion
		//delay 1000
 		println("handeObstacle: going back STOP")  
		//forward basicrobot -m robotCmd : robotCmd( h )
		forward resourcemodel -m modelChange : modelChange(robot,h) 
		*/
	}
	Goto waitCmd
 
 }
